# üöÄ Guia Pr√°tico de Desenvolvimento RAP no Eclipse (ADT) com Fiori Elements

Este documento √© um guia educacional sobre como criar aplica√ß√µes **SAP Fiori Elements** usando o **RAP (Restful ABAP Programming Model)** no **ABAP Development Tools (ADT)**.

---

## üìñ 1. Introdu√ß√£o

O **RAP** √© o modelo de programa√ß√£o ABAP orientado a servi√ßos, projetado para criar APIs e aplica√ß√µes SAP Fiori de forma declarativa, escal√°vel e reutiliz√°vel.  
Ele permite:
- Criar **CDS Views** para modelagem de dados
- Definir **comportamento** com Behavior Definitions
- Expor dados via **OData Services**
- Configurar **UI** diretamente no backend com anota√ß√µes

No desenvolvimento RAP com Fiori Elements, seguimos normalmente esta ordem:

1. **Modelar os dados** (Interface e Consumption Views)
2. **Definir apresenta√ß√£o** (Metadata Extensions)
3. **Expor servi√ßo** (Service Definition + Service Binding)
4. **Definir comportamento** (Behavior Definition/Implementation)
5. **Testar e publicar** (Fiori Launchpad/App Manager)

---

## üìÇ 2. Modelagem de Dados (CDS Views)

As CDS Views (Core Data Services) representam a camada de dados e podem conter **anota√ß√µes** que afetam como os dados ser√£o exibidos e consumidos.

### 2.1 Interface View (ZI\_...)
Camada base que encapsula tabelas, define associa√ß√µes e estrutura principal dos dados.  
N√£o √© exposta diretamente ao frontend.

**Exemplo ‚Äì Cabe√ßalho:**
```abap
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Ordem de Vendas - Cabe√ßalho - Eduardo'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{ serviceQuality: #X, sizeCategory: #S, dataClass: #MIXED }
@Search.searchable: true

define root view entity ZI_HEADEROV_ESCHOEPF 
    as select from I_SalesOrder
    composition [1..*] of ZI_ITEMOV_ESCHOEPF as _SalesOrderItem
{
    @Search.defaultSearchElement: true
    key SalesOrder           as SalesOrder,
        SalesOrderType       as SalesOrderType,
        CreatedByUser        as CreatedByUser,
        LastChangedByUser    as LastChangedByUser,
        CreationDate         as CreationDate,
        SalesOrganization    as SalesOrganization,
        DistributionChannel  as DistributionChannel,
        OrganizationDivision as OrganizationDivision,
        SalesGroup           as SalesGroup,
        SalesOffice          as SalesOffice,
        @Semantics.amount.currencyCode: 'TransactionCurrency'
        TotalNetAmount       as TotalNetAmount,
        TransactionCurrency  as TransactionCurrency,
        
        case
          when  TotalNetAmount  > 10000 then '3' -- Verde
          when  TotalNetAmount  > 0 then '2'     -- Amarelo
          else '1'                               -- Vermelho
        end as Criticality,
        
        _SalesOrderItem
}

```

**Exemplo ‚Äì Item:**
```abap
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Ordem de vendas - Item - Eduardo'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZI_ITEMOV_ESCHOEPF 
    as select from I_SalesOrderItem 
    association to parent ZI_HEADEROV_ESCHOEPF as _SalesOrder
    on $projection.SalesOrder = _SalesOrder.SalesOrder
{
    key SalesOrder             as SalesOrder,
    key SalesOrderItem         as SalesOrderItem,
        SalesOrderItemCategory as SalesOrderItemCategory,
        SalesOrderItemType     as SalesOrderItemType,
        IsReturnsItem          as IsReturnsItem,
        CreatedByUser          as CreatedByUser,
        CreationDate           as CreationDate,
        CreationTime           as CreationTime,
        LastChangeDate         as LastChangeDate,
        Division               as Division,
        Material               as Material,
        Batch                  as Batch,
        Plant                  as Plant,
        StorageLocation        as StorageLocation,
        @Semantics.quantity.unitOfMeasure: 'OrderQuantityUnit'
        OrderQuantity          as OrderQuantity, 
        OrderQuantityUnit      as OrderQuantityUnit,
        @Semantics.quantity.unitOfMeasure: 'ItemWeightUnit'
        ItemGrossWeight        as ItemGrossWeight,
        @Semantics.quantity.unitOfMeasure: 'ItemWeightUnit'
        ItemNetWeight          as ItemNetWeight,
        ItemWeightUnit         as ItemWeightUnit,
        @Semantics.quantity.unitOfMeasure: 'ItemVolumeUnit'
        ItemVolume             as ItemVolume,
        ItemVolumeUnit         as ItemVolumeUnit,
        @Semantics.amount.currencyCode: 'TransactionCurrency'
        NetAmount              as NetAmount,
        TransactionCurrency    as TransactionCurrency,
      
        _SalesOrder    
       
}
```

### 2.2 Consumption View (ZC_...)

Camada de proje√ß√£o que exp√µe dados ao frontend (Fiori Elements), contendo anota√ß√µes para UI, formata√ß√£o e filtragem.  

**Exemplo ‚Äì Cabe√ßalho:**
```abap
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Ordem de Venda - Cabe√ßalho - ESCHOEPF'
@Metadata.ignorePropagatedAnnotations: false
@Metadata.allowExtensions: true
define root view entity ZC_HEADEROV_ESCHOEPF
  provider contract transactional_query
    as projection on ZI_HEADEROV_ESCHOEPF
{
    @ObjectModel.text.element: [ 'SalesOrderType' ]
    @UI.textArrangement: #TEXT_LAST
    key SalesOrder           as SalesOrder,
        SalesOrderType       as SalesOrderType,
        CreatedByUser        as CreatedByUser,
        LastChangedByUser    as LastChangedByUser,
        CreationDate         as CreationDate,
        SalesOrganization    as SalesOrganization,
        DistributionChannel  as DistributionChannel,
        OrganizationDivision as OrganizationDivision,
        SalesGroup           as SalesGroup,
        SalesOffice          as SalesOffice,   
        TotalNetAmount       as TotalNetAmount,
        TransactionCurrency  as TransactionCurrency,
        Criticality          as Criticality, 
        
        _SalesOrderItem : redirected to composition child ZC_ITEMOV_ESCHOEPF
}
```
**Exemplo ‚Äì Item:**
```abap
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Ordem de Vendas - Item -ESCHOEPF'
@Metadata.ignorePropagatedAnnotations: false
@Metadata.allowExtensions: true

define view entity ZC_ITEMOV_ESCHOEPF 
    as projection on ZI_ITEMOV_ESCHOEPF
{
    key SalesOrder             as SalesOrder,
    key SalesOrderItem         as SalesOrderItem,
        SalesOrderItemCategory as SalesOrderItemCategory,
        SalesOrderItemType     as SalesOrderItemType,
        IsReturnsItem          as IsReturnsItem,
        CreatedByUser          as CreatedByUser,
        CreationDate           as CreationDate,
        CreationTime           as CreationTime,
        LastChangeDate         as LastChangeDate,
        Division               as Division,
        Material               as Material,
        Batch                  as Batch,
        Plant                  as Plant,
        StorageLocation        as StorageLocation,
        @Semantics.quantity.unitOfMeasure: 'OrderQuantityUnit'
        OrderQuantity          as OrderQuantity, 
        OrderQuantityUnit      as OrderQuantityUnit,
        @Semantics.quantity.unitOfMeasure: 'ItemWeightUnit'
        ItemGrossWeight        as ItemGrossWeight,
        @Semantics.quantity.unitOfMeasure: 'ItemWeightUnit'
        ItemNetWeight          as ItemNetWeight,
        ItemWeightUnit         as ItemWeightUnit,
        @Semantics.quantity.unitOfMeasure: 'ItemVolumeUnit'
        ItemVolume             as ItemVolume,
        ItemVolumeUnit         as ItemVolumeUnit,
        @Semantics.amount.currencyCode: 'TransactionCurrency'
        NetAmount              as NetAmount,
        TransactionCurrency    as TransactionCurrency,
      
        _SalesOrder   : redirected to parent ZC_HEADEROV_ESCHOEPF
}
```  

### üé® 3. Configura√ß√£o de UI (Metadata Extensions)

Usada para modularizar e centralizar a configura√ß√£o da interface no Fiori Elements, sem poluir a CDS View principal.

üìå Recomendada para manter o c√≥digo limpo e reutiliz√°vel.

**Exemplo ‚Äì Cabe√ßalho:**
```abap
@Metadata.layer: #CORE
@UI.headerInfo: { typeName: 'Pedidos de compra',
                  typeNamePlural: 'Pedidos de compra' }
                 
@UI.presentationVariant: [{ sortOrder: [{by: 'TotalNetAmount', direction: #DESC }] }]
                  
annotate entity ZC_HEADEROV_ESCHOEPF
    with 
{
    
    @UI.facet: [{
        id: 'header',
        label: 'Cabe√ßalho',
        position: 10,
        type: #FIELDGROUP_REFERENCE, //ou #IDENTIFICATION_REFERENCE
        targetQualifier: 'HEADER'
    }, {
        id: 'items',                        // Adiciona a faceta para os itens
        label: 'Itens da Ordem de Vendas',  // Nome da faceta
        position: 20,                       // A posi√ß√£o da faceta na UI
        type: #LINEITEM_REFERENCE,
        targetElement: '_SalesOrderItem'    // Qualificador de campo para os itens
    }]
     
    // Campos do cabe√ßalho da ordem de vendas
    //@Consumption.filter.mandatory: true         
    @UI.lineItem: [{ position: 10, label: 'N√∫mero da Ordem de Vendas' }]
    @UI.selectionField: [{ position: 10 }]
    @EndUserText.label: 'Pedido de compras'
    @Consumption.valueHelpDefinition: [{ entity.name: 'I_MM_SalesOrderItemVH', entity.element: 'SalesOrder' }]
    @UI.fieldGroup: [{ position: 10, qualifier: 'HEADER' }]  // Posi√ß√£o 10, agrupando no cabe√ßalho
    SalesOrder;

    @UI.selectionField: [{ position: 20 }]
    @UI.lineItem: [{ position: 20 }]
    @Consumption.valueHelpDefinition: [{ entity.name: 'I_SalesOrderType', entity.element: 'SalesOrderType' }]
    @UI.fieldGroup: [{ position: 20, qualifier: 'HEADER' }]  // Posi√ß√£o 20, agrupando no cabe√ßalho
    SalesOrderType;

    @UI.lineItem: [{ position: 30 }]
    @UI.fieldGroup: [{ position: 30, qualifier: 'HEADER' }]  // Posi√ß√£o 30, agrupando no cabe√ßalho
    CreatedByUser;

    @UI.lineItem: [{ position: 40 }]
    @UI.fieldGroup: [{ position: 40, qualifier: 'HEADER' }]  // Posi√ß√£o 40, agrupando no cabe√ßalho
    LastChangedByUser;

    @UI.selectionField: [{ position: 30 }]
    @UI.lineItem: [{ position: 50 }]
    @Consumption.filter: { selectionType: #RANGE }
    @UI.fieldGroup: [{ position: 50, qualifier: 'HEADER' }]  // Posi√ß√£o 50, agrupando no cabe√ßalho
    CreationDate;

    @UI.lineItem: [{ position: 60 }]
    @UI.fieldGroup: [{ position: 60, qualifier: 'HEADER' }]  // Posi√ß√£o 60, agrupando no cabe√ßalho
    SalesOrganization;

    @UI.lineItem: [{ position: 70 }]
    @UI.fieldGroup: [{ position: 70, qualifier: 'HEADER' }]  // Posi√ß√£o 70, agrupando no cabe√ßalho
    DistributionChannel;

    @UI.lineItem: [{ position: 80 }]
    @UI.fieldGroup: [{ position: 80, qualifier: 'HEADER' }]  // Posi√ß√£o 80, agrupando no cabe√ßalho
    OrganizationDivision;

    @UI.lineItem: [{ position: 90 }]
    @UI.fieldGroup: [{ position: 90, qualifier: 'HEADER' }]  // Posi√ß√£o 90, agrupando no cabe√ßalho
    SalesGroup;

    @UI.lineItem: [{ position: 100 }]
    @UI.fieldGroup: [{ position: 100, qualifier: 'HEADER' }]  // Posi√ß√£o 100, agrupando no cabe√ßalho
    SalesOffice;
    
    @UI.lineItem: [{ position: 110, criticality: 'Criticality' }]
    @UI.fieldGroup: [{ position: 110, qualifier: 'HEADER', criticality: 'Criticality' }]  // Posi√ß√£o 110, agrupando no cabe√ßalho
    TotalNetAmount;
    
    @UI.lineItem: [{ position: 120 }]
    @UI.hidden: true
    @UI.fieldGroup: [{ position: 120, qualifier: 'HEADER' }]  // Posi√ß√£o 120, agrupando no cabe√ßalho
    TransactionCurrency;
    
    // Faceta para os itens da ordem de vendas
    @UI.fieldGroup: [{ position: 140, qualifier: 'ITEMS' }]  // Posi√ß√£o 140 para os itens da ordem de vendas
    _SalesOrderItem; // Esta √© a associa√ß√£o que traz os itens da ordem de vendas
    
}
```

**Exemplo ‚Äì Item:**
```abap
@Metadata.layer: #CORE
annotate entity ZC_ITEMOV_ESCHOEPF with
{
      @UI.lineItem: [{ position: 10 }]
      SalesOrder;
      @UI.lineItem: [{ position: 20 }]
      SalesOrderItem;
      @UI.lineItem: [{ position: 30 }]
      SalesOrderItemCategory;
      @UI.lineItem: [{ position: 40 }]
      SalesOrderItemType;
      @UI.lineItem: [{ position: 50 }]
      IsReturnsItem;
      @UI.lineItem: [{ position: 60 }]
      CreatedByUser;
      @UI.lineItem: [{ position: 70 }]
      CreationDate;
      @UI.lineItem: [{ position: 80 }]
      CreationTime;
      @UI.lineItem: [{ position: 90 }]
      LastChangeDate;
      @UI.lineItem: [{ position: 100 }]
      Division;
      @UI.lineItem: [{ position: 110 }]
      Material;
      @UI.lineItem: [{ position: 120 }]
      Batch;
      @UI.lineItem: [{ position: 130 }]
      Plant;
      @UI.lineItem: [{ position: 140 }]
      StorageLocation;
      @UI.lineItem: [{ position: 150 }]
      OrderQuantity;
      @UI.lineItem: [{ position: 160 }]
      OrderQuantityUnit;
      @UI.lineItem: [{ position: 170 }]
      ItemGrossWeight;
      @UI.lineItem: [{ position: 180 }]
      ItemNetWeight;
      @UI.lineItem: [{ position: 190 }]
      ItemWeightUnit;
      @UI.lineItem: [{ position: 200 }]
      ItemVolume;
      @UI.lineItem: [{ position: 210 }]
      ItemVolumeUnit;
      @UI.lineItem: [{ position: 220 }]
      NetAmount;
      @UI.lineItem: [{ position: 230 }]
      TransactionCurrency;
}
```

### üåê 4. Exposi√ß√£o OData (Service Definition e Binding)

**Service Definition**
Seleciona quais entidades (Consumption Views) ser√£o expostas:
```abap
@EndUserText.label: 'Ordem de vendas'
define service ZUI_SALESORDER_ESCHOEPF {
  expose ZC_HEADEROV_ESCHOEPF as SalesOrder;
  expose ZC_ITEMOV_ESCHOEPF   as SalesOrderItem;
}
```

**Service Binding**

- Vincula a Service Definition a um protocolo (OData V4 recomendado)  
- Gera automaticamente o endpoint  
- Ap√≥s ativar, a URL √© fornecida pelo sistema

---

## ‚öôÔ∏è 5. Defini√ß√£o de Comportamento (Behavior Definition & Implementation)

Necess√°ria em **apps transacionais** (CRUD).  
Permite configurar:
- Opera√ß√µes permitidas (create, update, delete)
- Valida√ß√µes
- Determina√ß√µes
- Controle de concorr√™ncia

**Exemplo Behavior Definition:**
```abap
CLASS zbp_i_productroot_eschoepf DEFINITION PUBLIC ABSTRACT FINAL FOR BEHAVIOR OF zi_productroot_eschoepf.
ENDCLASS.

CLASS zbp_i_productroot_eschoepf IMPLEMENTATION.
ENDCLASS.
```

```abap
// In√≠cio da implementa√ß√£o gerenciada de um behavior definition (defini√ß√£o de comportamento)
managed implementation in class zbp_i_productroot_eschoepf unique;

// Define que a implementa√ß√£o √© *strict*, ou seja, segue regras mais r√≠gidas de verifica√ß√£o (vers√£o 2 das regras)
strict ( 2 );

// Define o *behavior* (comportamento) para a interface de dados ZI_PRODUCTROOT_ESCHOEPF
define behavior for ZI_PRODUCTROOT_ESCHOEPF alias Products

// Informa a tabela persistente que armazena os dados no banco de dados
persistent table ztb_product_esch

// Define o controle de bloqueio de registros como *master*, ou seja, ao editar um item, ele ser√° bloqueado
lock master

// Controle de autoriza√ß√£o no n√≠vel da inst√¢ncia (registro por registro)
authorization master ( instance )

// Define um campo para controle de concorr√™ncia otimista.
// Isso evita que duas pessoas atualizem o mesmo registro ao mesmo tempo sem saber.
// O campo 'LocalLastChangeAt' armazena a √∫ltima data/hora de modifica√ß√£o do registro.
// Antes de salvar uma altera√ß√£o, o sistema verifica se o valor ainda √© o mesmo.
// Se algu√©m j√° tiver alterado o registro, a atualiza√ß√£o √© bloqueada para evitar perda de dados.
etag master LocalLastChangeAt

// Bloco de a√ß√µes permitidas para essa entidade:
// Isso define que os registros podem ser criados, atualizados e deletados via o servi√ßo RAP
{
  create;   // Permite criar novos registros
  update;   // Permite atualizar registros existentes
  delete;   // Permite excluir registros


  determination setProduct on modify { create; }
  validation    checkNetWeight on save { field NetWeight; create; update; }

  field ( numbering : managed, readonly ) ProductUuid;
  field ( readonly ) Product;
  field ( mandatory ) ProductDescription, Plant;

  mapping for ztb_product_esch
  {
        ProductUuid        = product_uuid;
        Product            = matnr;
        Plant              = werks;
        ProductDescription = maktx;
        NetWeight          = ntgew;
        WeightUnit         = gewei;
        LastChangeBy       = last_changed_by;
        LastChangeAt       = last_changed_at;
        LocalLastChangeAt  = local_last_changed_at;
  }
}
```

```abap
projection;
strict ( 2 );

define behavior for ZC_PRODUCTROOT_ESCHOEPF alias Products
{
  use create;
  use update;
  use delete;
}

```

üöÄ Deploy e Teste no Fiori Launchpad
Com o servi√ßo ativo e anotado corretamente, o app Fiori Elements pode ser consumido via Launchpad (FLP), App Manager, ou integrado em cat√°logos.  


## üìÇ Estrutura gen√©rica de arquivos no ABAP Development Tools (ADT)
```
/sap-fiori-elements
‚îÇ
‚îú‚îÄ‚îÄ /Dictionary
‚îÇ   ‚îî‚îÄ‚îÄ /Database Tables         # Tabelas com campos persistentes que representam as entidades de neg√≥cio
‚îÇ
‚îú‚îÄ‚îÄ /Core Data Services
‚îÇ   ‚îú‚îÄ‚îÄ /Data Definitions        # CDS Views (Interface + Consumption)
‚îÇ   ‚îÇ   ‚îú‚îÄ‚îÄ ZI_Entity            # Interface View: encapsula as tabelas e define associa√ß√µes (`association [0..*] to`)
‚îÇ   ‚îÇ   ‚îî‚îÄ‚îÄ ZC_Entity            # Consumption View: camada de proje√ß√£o (projeta os dados para o consumo)
‚îÇ   ‚îÇ
‚îÇ   ‚îú‚îÄ‚îÄ /Behavior Definitions    # Comportamento das entidades RAP
‚îÇ   ‚îÇ   ‚îî‚îÄ‚îÄ  ZI_Entity           # Behavior Definition (CRUD, valida√ß√µes simples)
‚îÇ   ‚îÇ
‚îÇ   ‚îî‚îÄ‚îÄ /Metadata Extensions     # Anota√ß√µes UI (@UI)
‚îÇ       ‚îî‚îÄ‚îÄ ZC_Entity            # Define, por meio de anota√ß√µes `@UI`, como os dados ser√£o exibidos na interface (@UI.lineItem, @UI.FieldGroup, etc.)
‚îÇ
‚îî‚îÄ‚îÄ /Business Services
    ‚îú‚îÄ‚îÄ /Service Definitions     # Exposi√ß√£o OData
    ‚îÇ   ‚îî‚îÄ‚îÄ ZSD_EntityService    # Exposi√ß√£o das CDS Views como entidades OData
    ‚îÇ
    ‚îî‚îÄ‚îÄ /Service Bindings        # Ativa√ß√£o do servi√ßo
        ‚îî‚îÄ‚îÄ ZSB_EntityService    # Configura√ß√£o do protocolo (OData V2/V4) e tipo (UI)

```
